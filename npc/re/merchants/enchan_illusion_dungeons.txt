//===== rAthena Script =======================================
//= Episode 16.2 - Illusion Enchants
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Illusion series related merchants and enchanters
// Note:
// - Some dialog are missing in Illusion of Luanda exchange NPC.
//===== Changelog: ===========================================
//= 1.0 Initial release of Illusion of Moonlight [crazyarashi]
//= 1.1 Cleanup and improvements [Everade]
//= 1.2 Added Illusion of Vampire enchanter [Capuche]
//= 1.3 Added Illusion of Frozen enchanter [Capuche]
//= 1.4 Added Illusion of Turtle enchanter [Capuche]
//= 1.5 Added Illusion of Luanda enchanter [Capuche]
//= 1.6 Added Illusion of Underwater exchange [Capuche]
//= 1.7 Added Illusion of Twins enchanter [Capuche]
//= 1.8 Added Illusion Merchant [Haydrich]
//= 1.9 Added Illusion of Teddy Bear exchange [Atemo]
//============================================================

//============================================================
//= Illusion of Moonlight
//============================================================
pay_d03_i,160,45,3	script	Gemcutter#ilp20	4_TOWER_17,3,3,{
	mes "[ Gemcutter ]";
	mes "Do you have business with me?";
	next;
	switch( select( "What are you doing here?", "Upgrade equipments" ) ) {
	case 1:
		mes "[ Gemcutter ]";
		mes "I came to the ruined village,";
		mes "looking for some materials. Touched some strange light, and now I'm here.";
		next;
		mes "[ Gemcutter ]";
		mes "I've decided to stick around for a little while. Figured I'd be safe so long as I stay close to this soldier.";
		mes "I have a proposition for you. I want some materials that can only be found in this place.";
		next;
		mes "[ Gemcutter ]";
		mes "Get them for me, and I'll improve your equipment in exchange. Just so you know, I can only improve certain types.";
		next;
		mes "[ Gemcutter ]";
		mes "If you're interested, we can discuss the details of our bargain.";
		close;
	case 2:
		mes "[ Gemcutter ]";
		mes "Make sure ^0000FFyour equipment is refined to at least +9^000000 before bringing it to me.";
		mes "That's the minimum requirement for my upgrade service to have any visible effects on your equipment.";
		next;
		mes "[ Gemcutter ]";
		mes "Make sure you're ^0000FFequipped with the item that you want to improve.^000000";
		mes "Otherwise, I can't evaluate its condition.";
		next;
		mes "[ Gemcutter ]";
		mes "As you may have guessed, your equipment will transform into something new after this.";
		mes "In other words, ^0000FFIt'll lose its current refining levels, cards and enchantments.^000000";
		next;
		mes "[ Gemcutter ]";
		mes "And I need ^0000FFIllusion Stones and some other materials^000000 to upgrade your equipment.";
		mes "Pick an item you want and I'll tell you what I need.";
		close2;
		callshop( "barter_ill_moonlight" );
		end;
	}
	end;

OnTouch:
	if (illusion_moonlight > 7)
		npctalk "What kind of place is this?", "", bc_self;
	end;
}


//============================================================
//= Illusion of Vampire
//============================================================
gef_dun01,139,228,3	script	Great Merchant#illgef	4_M_HUMERCHANT,{
	mes "[Great Merchant]";
	mes "Adventurer, do you have ^0000cdIllusion Stones^000000? If you have a ^0000cdweapon, a piece armor^000000, or ^0000cdan accessory at Refining Level 9 or higher^000000, ";
	mes "then I can exchange it for something better at the cost of some Illusion Stones and other materials.";
	next;
	mes "[Great Merchant]";
	mes "So, what do you want?";
	next;
	switch( select( "Exchange illusion equipment", "What are Illusion Stones?", "Can I strengthen the exchanged item?", "Cancel" ) ) {
	case 1:
		specialeffect EF_HFLIMOON3;
		emotion ET_BEST;
		mes "[Great Merchant]";
		mes "Use it well and come see me later if you find more Illusion Stones! *Chuckle*";
		close2;
		callshop( "barter_ill_vampire" );
		end;
	case 2:
		mes "[Great Merchant]";
		mes "What are ^0000cdIllusion Stones^000000, you ask? Well... I don't think anyone knows exactly what they are.";
		next;
		mes "[Great Merchant]";
		mes "I only know they're rare and can only be found in some special places, and I'm here to collect them for my clients.";
		next;
		mes "[Great Merchant]";
		mes "My clients want to figure out what these stones are. They're paying me a lot of money,";
		next;
		mes "[Great Merchant]";
		mes "so I could offer adventurers like you ^0000cdexpensive equipment in exchange for the stones^000000, and still fetch a profit.";
		next;
		mes "[Great Merchant]";
		mes "Bring ^0000cda piece of refined equipment, Illusion Stones^000000, and various materials that are only found in this place. I'll ^0000cdupgrade the equipment, weapon, armor, or accessory?^000000for you.";
		next;
		mes "[Great Merchant]";
		mes "This benefits both of us. Let me know if you're interested in my proposition.";
		close;
	case 3:
		mes "[Great Merchant]";
		mes "So, you want to reinforce the equipment you get. You're thorough. I like that!";
		next;
		mes "[Great Merchant]";
		mes "A while ago, an adventurer who passed through this place told me that a chemist in Prontera refines the Illusion equipment in exchange for Illusion Stones.";
		next;
		mes "[Great Merchant]";
		mes "It seems Illusion Stones are a popular topic everywhere. Everyone wants to know about what they are.";
		next;
		mes "[Great Merchant]";
		mes "If you want to refine your equipment, then go to the <NAVI>Illusion Enchanter<INFO>prontera,90,115,000,0,</INFO></NAVI> near the Town Office.";
		close;
	case 4:
		mes "[Great Merchant]";
		mes "Did you find anything you liked? If you need anything, please let me know at any time.";
		close;
	}
	end;
}

//============================================================
//= Illusion of Frozen
//============================================================

// illusion exchange npc - trade items for illusion gears
ice_dun02,153,18,3	script	Illusion Stone Research	4_M_ALCHE_B,{
	disable_items;
	mes "[Illusion Stone Researcher]";
	mes "Ah, Illusion Stones are truly mysterious...";
	next;
	switch( select( "What are you doing here?", "Upgrade equipments", "Cancel" ) ) {
	case 1:
		mes "[Illusion Stone Researcher]";
		mes "I'm here to find some Illusion Stones that I need for my research.";
		next;
		mes "[Illusion Stone Researcher]";
		mes "This cave is very mysterious, but I'm not strong enough to explore it by myself.";
		mes "If you go in there, could you procure some things for me?";
		next;
		mes "[Illusion Stone Researcher]";
		mes "In exchange, I'll improve your equipment.";
		mes "What do you say?";
		close;
	case 2:
		mes "[Illusion Stone Researcher]";
		mes "Make sure ^4d4dffyour equipment is refined to at least +9^000000 before bringing it to me.";
		mes "Make sure you're ^4d4dffequipped with the item that you want to improve^000000.";
		next;
		mes "[Illusion Stone Researcher]";
		mes "As you may have guessed, your equipment will transform into something new after this.";
		mes "In other words, ^4d4dffit'll lose its current Refining and Upgrade levels.^000000";
		next;
		mes "[Illusion Stone Researcher]";
		mes "And I need ^4d4dffIllusion Stones and some other materials^000000 to upgrade your equipment.";
		mes "Pick an item you want. I'll tell you what I need.";
		close2;
		callshop( "barter_ill_frozen" );
		end;
	case 3:
		mes "[Illusion Stone Researcher]";
		mes "It's just hard to get..";
		close;
	}
	end;
}


// illusion enhancing - enhance the illusion gears
// note: unknown enhance rate
prontera,90,115,5	script	Illusion Enchanter#prontera	677,{
	disable_command;
	disable_items;
	.@npc$ = "[ ^0000FFIllusion Enchanter^000000 ]";
	if(!metIlluEnchant){
		mes .@npc$,
			"Oh, did you bring Illusion equipment?";
		next;
		select("I don't know what those are...");
		mes .@npc$,
			"I was in a hurry, I should have explained that first.";
		next;
		mes .@npc$,
			"I am an alchemist studying a substance called the Illusion Stone. It's been circulating among adventurers lately.";
		next;
		select("Is this an Illusion?");
		mes .@npc$,
			"We don't know yet, but it's quite interesting.";
		next;
		select("That's amazing!");
		mes .@npc$,
			"I'm guessing you've heard about strange spaces that have opened all over the world. Illusion stones are rare occurrences that appear inside these spaces.";
		next;
		mes .@npc$,
			"Their atomic structure is not consistent with any minerals that I've came across so far...";
		next;
		select("Tell me more!");
		mes .@npc$,
			"To cut the chase, if you bring me Illusion stones, I can upgrade certain pieces of equipment for you.";
		next;
		select("What's the catch?");
		mes .@npc$,
			"There is no danger to the enchanting process. It cannot break your items unless you decide to reset the enchantments.";
		next;
		mes .@npc$,
			"I'll continue to study these Illusion dungeons to be able to create even more powerful equipments!";
		next;
		mes .@npc$,
			"Though, maybe I should have mentioned this earlier... This isn't a free service, I'll need Zeny and Illusion Stones.";
		next;
		mes .@npc$,
			"Did you get all that?";
		next;
		select("Yeah!");
		metIlluEnchant = 1;
		mes .@npc$,
			"Alright then! Let's get started.";
		close;
	}
	
	mes .@npc$,
		"I only enchant equipment that you have obtained from exchanging Illusion Stones.";
	next;
	switch(select("What do I need to enchant?", "Enchant Illusion equipment", "Reset Illusion equipment")){
		case 1: // What do I need to enchant?
			mes .@npc$,
				"Remember, I only enchant equipment that you have obtained from exchanging Illusion Stones.";
			next;
			mes .@npc$,
				"You can enchant each item twice, paying 5 Illusion Stones per enchant.";
			next;
			mes .@npc$,
				"There is no chance of failure when enchanting, but there is a chance of failure when you reset the item. This is just how the process works.";
			next;
			mes .@npc$,
				"It costs Zeny to reset the enchants, but the chance of success depends on the amount of Zeny you give me. Give it a shot.";
			close;
		
		case 2: // Enchant Illusion equipment
			
			for(.@i=0;.@i<10;.@i++){
				if (!getequipisequiped(.@i))
					continue;
				.@eqId = getequipid(.@i);
				if (inarray(.MeleeWeaponIds,.@eqId) == -1 && 
					inarray(.RangeWeaponIds,.@eqId) == -1 && 
					inarray(.MagicWeaponIds,.@eqId) == -1 && 
					inarray(.AccessoryIds,.@eqId) == -1 && 
					inarray(.ArmorIds,.@eqId) == -1)
					continue;
				.@eqSlots[getarraysize(.@eqSlots)] = .@i;
				.@cItems[getarraysize(.@cItems)] = .@eqId;
				.@menu$ += getitemname(.@eqId) + ":";
				.@count++;
			}
			if(.@count == 0){
				mes .@npc$,
					"You don't have any Illusion equipment equipped.";
				close;			
			}
			.@s = select(.@menu$) - 1;
			.@slot = .@eqSlots[.@s];
			.@equip_id = .@cItems[.@s];
			.@equip_name$ = getitemname(.@equip_id);
			setarray .@card[0],
				getequipcardid(.@slot,0),
				getequipcardid(.@slot,1),
				getequipcardid(.@slot,2),
				getequipcardid(.@slot,3);
			copyarray .@equip_card[0], .@card[0], 4;	// for final check
			.@equip_refine = getequiprefinerycnt(.@slot);
			
			if (.@card[3] > 0) {
				mes .@npc$;
				mes .@equip_name$ + " have already passed the enchanting limit. We can't enchant them any more.";
				close;
			}
			if(.@card[2] == 0)
				.@cardSlot = 2;
			else if(.@card[3] == 0)
				.@cardSlot = 3;
				
			if (.@card[.@cardSlot] == 0) {
				.@cost = .costStones;
				mes .@npc$;
				mes "Want to enchant ^0000FF" + .@equip_name$ + "^000000?";
				mes "For enchanting, you need ^0000FF" + .@cost + "^000000 Illusion Stones.";
				next;
				.@s2 = select("Enchant", "Quit") - 2;
				if (.@s2 == 0) {
					mes .@npc$;
					mes "Ok, come back when you are ready.";
					close;
				}
				if (inarray(.MeleeWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .MeleeWeaponEn[rand(0,getarraysize(.MeleeWeaponEn)-1)];
				else if (inarray(.RangeWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .RangeWeaponEn[rand(0,getarraysize(.RangeWeaponEn)-1)];
				else if (inarray(.MagicWeaponIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .MagicWeaponEn[rand(0,getarraysize(.MagicWeaponEn)-1)];
				else if (inarray(.AccessoryIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .AccessoryEnchants[rand(0,getarraysize(.AccessoryEnchants)-1)];
				else if (inarray(.ArmorIds,.@equip_id) != -1)
					.@card[.@cardSlot] = .ArmorEnchants[rand(0,getarraysize(.ArmorEnchants)-1)];
				.@string$ = "enchant number ^630000" + (.@cardSlot - 1) + "^000000.";
			}
			
			if (.@cost > countitem(25271)){
				mes .@npc$;
				mes "Hmm, you are missing " + (.@cost - countitem(25271)) + " Illusion Stones. Go get more, and then we can talk about more enchants.";
				close;
			}
			
			specialeffect2 EF_REPAIRWEAPON;
			delitem 25271,.@cost;// Illusion Stones
			
			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || 
				callfunc("F_IsEquipRefineHack", .@slot, .@equip_refine))
				close;
				
			delequip .@slot;
			mes .@npc$;
			mes "Trying for " + .@string$;
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
			close;
		case 3: // Reset Illusion equipment
			for(.@i=0;.@i<10;.@i++){
				if (!getequipisequiped(.@i))
					continue;
				.@eqId = getequipid(.@i);
				if (inarray(.MeleeWeaponIds,.@eqId) == -1 && 
					inarray(.RangeWeaponIds,.@eqId) == -1 && 
					inarray(.MagicWeaponIds,.@eqId) == -1 && 
					inarray(.AccessoryIds,.@eqId) == -1 && 
					inarray(.ArmorIds,.@eqId) == -1)
					continue;
				.@eqSlots[getarraysize(.@eqSlots)] = .@i;
				.@cItems[getarraysize(.@cItems)] = .@eqId;
				.@menu$ += getitemname(.@eqId) + ":";
				.@count++;
			}
			
			if(.@count == 0){
				mes .@npc$,
					"You don't have any Illusion equipment equipped.";
				close;			
			}
			
			.@s = select(.@menu$) - 1;
			.@slot = .@eqSlots[.@s];
			.@equip_id = .@cItems[.@s];
			.@equip_name$ = getitemname(.@equip_id);
			setarray .@card[0],
				getequipcardid(.@slot,0),
				getequipcardid(.@slot,1),
				getequipcardid(.@slot,2),
				getequipcardid(.@slot,3);
			copyarray .@equip_card[0], .@card[0], 4;	// for final check
			.@equip_refine = getequiprefinerycnt(.@slot);
			
			if (.@card[2] == 0) {
				mes .@npc$;
				mes .@equip_name$ + " Doesn't have any enchants..";
				close;
			}
			
			mes .@npc$,
				"So you want to reset all the slots..";
			next;
			mes .@npc$,
				"How much are you gonna to pay me to reset the item for you?";
			next;
			.@p = select("5 Illusion Stones", "100,000 zeny", "200,000 zeny", "300,000 zeny", "400,000 zeny", "500,000 zeny");
			.@chance = 0;
			if(.@p != 1){
				.@amt = .@p * 100000;
				if(.@p < 5){
					mes .@npc$,
						"Only " + F_InsertComma(.@amt) + " zeny...";
					.@chance = 40 + (.@p*10);
				}else{
					.@chance = 100;
					mes .@npc$,
						"W-wow, " + F_InsertComma(.@amt) + " zeny!";
					next;
					mes .@npc$,
						"I've never seen this much before.";
				}
			}else {
				.@amt = 5;
				.@chance = 100;
				mes .@npc$,
					"Illusion Stones!!";
				emotion ET_SPARK,getnpcid(0,"Illusion Enchanter#prontera");
			}
			next;
			mes .@npc$,
				"The chance of success is " + .@chance + "%";
			next;
			if(.@chance != 100){
				mes .@npc$,	
					"These is a chance that the item could break and you can lose the item AND the cards!";
				next;
				mes .@npc$,
					"Are you sure that you want to continue?";
				next;
				if(select("No. Cancel.", "Yes. I'm sure.") == 1){
					mes .@npc$,
						"Alright, come back when you're confidant.";
					close;
				}
			}
			
			mes .@npc$,
				"This will cost:";
			if(.@p == 1)
				mes .@amt + " Illusion Stones.";
			else
				mes F_InsertComma(.@amt) + " Zeny";
			next;
			
			if (select("Continue.","Cancel.") == 2){
				mes .@npc$,
					"Alright, come back when you're confidant.";
				close;
			}
			
			if(.@p == 1){
				if(.@amt > countitem(25271)){
					mes .@npc$,
						"You don't have "+.@amt+" Illusion Stones.";
					close;
				}
				delitem 25271,.@amt;
			}else{
				if(.@amt > Zeny){
					mes .@npc$,
						"You don't have enough Zeny.",
						"You require " + F_InsertComma(.@amt) + " zeny";
					close;
				}
				Zeny -= .@amt;
			}
			
			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@slot, .@equip_id) || callfunc("F_IsEquipCardHack", .@slot, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || 
				callfunc("F_IsEquipRefineHack", .@slot, .@equip_refine))
				close;
				
			.@c = rand(100);
			if(.@chance >= .@c){
				specialeffect2 EF_REPAIRWEAPON;
				delequip .@slot;
				mes .@npc$;
				mes "The process was a success!";
				getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],0,0;
				close;
			}
			
			specialeffect2 EF_REFINEFAIL;
			delequip .@slot;
			mes .@npc$;
			mes "Sorry, but it seems I didn't put much effort into the process...";
		close;
	}
	end;
OnInit:
	.costStones = 5;
	setarray .MeleeWeaponIds,	28023;

	setarray .AccessoryIds,		490121,490120,490070,490069,32239,32238,28509,28508;
	setarray .ArmorIds,			400053,480054,450145,450144,
								19428,20948,
								450182,460018,460017,470066,
								19366,15348,20923,22192,							
								19223,20847,28922,
								19344,20840,22190,
								19210,19209,15195,20838,22133;
	
	setarray .AccessoryEnchants,4795,4796,4797,4798,	//HP 100~400
								4928,4870,4800,4871,	//SP 10~75
								4700,4701,4702,4703,	//STR 1~4
								4730,4731,4732,4733,	//AGI 1~4
								4740,4741,4742,4743,	//VIT 1~4
								4710,4711,4712,4713,	//INT 1~4
								4720,4721,4722,4723,	//DEX 1~4
								4750,4751,4752,4753;	//LUK 1~4
	
	setarray .ArmorEnchants,	4861,4862,4863,4864,	//Max HP 1~4%
								4993,29208,4992,29210,	//HP/SP Absorption  1~2%
								4994,4995,4997,4998,	//strength/agility Lv 1~2
								29009,29010,29000,29001,//Vitality/Intellect Lv 1~2
								29003,29004,29006,29007,//Dexterity/Luck Lv 1~2
								4700,4701,4702,4703,	//STR 1~4
								4730,4731,4732,4733,	//AGI 1~4
								4740,4741,4742,4743,	//VIT 1~4
								4710,4711,4712,4713,	//INT 1~4
								4720,4721,4722,4723,	//DEX 1~4
								4750,4751,4752,4753;	//LUK 1~4
								
	setarray .MeleeWeaponEn,	4811,4810,4809,4808,4820,		//Fighting Spirit 1~5
								29081,29082,29083,29084,29085,	//Acute Lv 1~5
								29061,29062,29063,29064,29065,	//Mettle Lv 1~5
								4807,4842,						//ASPD 1 2
								4700,4701,4702,4703,			//STR 1~4
								4720,4721,4722,4723,			//DEX 1~4
								4750,4751,4752,4753;			//LUK 1~4
	
	setarray .RangeWeaponEn,	4832,4833,4834,4835,4836,		// Expert Archer 1~5
								29091,29092,29093,29094,29095,	//Master Archer Lv 1~5
								29071,29072,29073,29074,29075,	//Magic Essence Lv 1~5
								4807,4842,						//ASPD 1 2
								4730,4731,4732,4733,			//AGI 1~4
								4720,4721,4722,4723,			//DEX 1~4
								4750,4751,4752,4753;			//LUK 1~4
								
	setarray .MagicWeaponEn,	4815,4814,4813,4812,4826,	//Spell 1~5
								4805,4850,					// Archbishop Lv 1~2
								4710,4711,4712,4713,		//INT 1~4
								4720,4721,4722,4723,		//DEX 1~4
								4750,4751,4752,4753;		//LUK 1~4
end;
}

//============================================================
//= Illusion of Turtle
//============================================================

// Exchange npc
alberta,226,28,3	script	Equipment Researcher	4_TOWER_15,{
	.@illusion_stone_name$ = getitemname(25271);

	mes "[Equipment Researcher]";
	mes "I'm the one who researches " + .@illusion_stone_name$ + " to enhance armors and weapons.";
	mes "If you have the materials I need for my research, wouldn't you hand them over?";
	mes "If you give me some equipment and some materials, I'll exchange them for my reinforced equipment.";
	next;
	switch( select( "What are you doing here?", "Exchange equipment" ) ) {
	case 1:
		mes "[Equipment Researcher]";
		mes "I'm working on how to use an " + .@illusion_stone_name$ + " to strengthen equipment.";
		mes "I went to go to the place where " + .@illusion_stone_name$ + " was found, and I barely got back..";
		next;
		mes "[Equipment Researcher]";
		mes "I think I met a girl...";
		mes "Anyway, after that, I couldn't find a way to go back.";
		mes "Here, we're getting research materials from adventurers with " + .@illusion_stone_name$ + ", and I'm offering enhanced equipment instead.";
		close;
	case 2:
		mes "[Equipment Researcher]";
		mes "What are the equipment I can exchange? Wait a minute. I'll let you know.";
		next;
		mes "[Equipment Researcher]";
		mes "As a material, you can bring ^0000CDexisting equipment that have been refined to +9 or higher.^000000";
		mes "And I need some ^0000CD" + .@illusion_stone_name$ + " and other materials.^000000";
		next;
		mes "[Equipment Researcher]";
		mes "Of course, we're giving you new equipment, so don't forget that all the existing ^0000CDsmelting and enchantments^000000 will disappear.";
		mes "So, what kind of weapon do you want?";
		close2;
		callshop( "barter_ill_turtle" );
		end;
	}
}


//============================================================
//= Illusion of Luanda
//============================================================

// Exchange npc
com_d02_i,234,266,6	script	Village Soap#Lu	4_M_ORIENT01,{
	mes "[Village Soap]";
	mes "I've also rolled up my sleeves for the adventurers who work hard for the village. Can I make your equipment stronger? Is there anything on the list you want?";
	next;
	if (select( "Look at the illusion equipment", "Cancel" ) == 2) {
		mes "[Village Soap]";
		mes "Please come later. Just bring the ingredients and I'll make it for you.";
		close;
	}
	mes "[Village Soap]";
	mes "Who made it? It might be useful for you to take it with you.";
	close2;
	callshop( "barter_ill_luanda" );
	end;
}


//============================================================
//= Illusion of Underwater
//============================================================

iz_d04_i,134,228,4	script	Horen#Horen	4_M_BIBI,{
	if (checkweight(1201,3) == 0) {
		mes "Conversation cannot proceed because you have too many items.";
		mes "Please clean up your inventory and try again.";
		close;
	}
	mes "[Horen]";
	mes "Hello. If you have rare materials from this place, I can enhance your equipment.";
	next;
	mes "[Horen]";
	mes "Would you like to see upgradeable equipment?";
	next;
	if (select( "Yes", "No" ) == 2) {
		mes "[Horen]";
		mes "Come back if interested.";
		close;
	}
	mes "[Horen]";
	mes "There are many types, so take your time. If you want something, I can enhance it right away.";
	close2;
	callshop( "barter_ill_underwater" );
	end;
}

//============================================================
//= Illusion of Twins
//============================================================
// ant_d02_i,175,186,3	script	From#iltw	MECHANIC,{
ant_d02_i,175,186,3	script	From#iltw	HIDDEN_NPC,{
	if (checkweight(1201,3) == 0) {
		mes "- You cannot proceed with the quest because you have too many items. -";
		close;
	}
	mes "[From]";
	mes "I feel like I'm going crazy.";
	mes "It's because of my mood. I'm human and it's not like you're mistaken.";
	mes "I'll help you if you get a lot here.";
	mes "Do you want to enhance your equipment?";
	close2;
	callshop( "barter_ill_twins" );
	end;

OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id, UNPC_CLASS, 4058;
	setunitdata .@npc_id, UNPC_SEX, SEX_MALE;
	setunitdata .@npc_id, UNPC_CLOTHCOLOR, 1;
	setunitdata .@npc_id, UNPC_HAIRSTYLE, 11;
	setunitdata .@npc_id, UNPC_HAIRCOLOR, 6;
	end;
}

//============================================================
//= Illusion Merchant
//============================================================
-	marketshop	market_resonance_stone	-1,100003:2000000:99999,100004:2000000:99999
prontera,88,113,5	script	Illusion Merchant#0829	HIDDEN_NPC,{
	mes "[Illusion Merchant]";
	mes "I'm selling two random optional weapon grant scrolls that I made myself. You can choose the payment method you like.";
	next;
	switch( select( "Purchase with Zeny.", "Purchase with Illusion Stones" )) {
		case 1:
			mes "[Illusion Merchant]";
			mes "I hope you like it.";
			close2;
			callshop "market_resonance_stone";
			end;
		case 2:
			mes "[Illusion Merchant]";
			mes "I hope you like it.";
			close2;
			callshop "resonance_stone_barter";
			end;
	}
OnInit:
	.@npc_id = getnpcid(0);
	setunitdata .@npc_id,UNPC_CLASS, JOB_MAGE_HIGH;
	setunitdata .@npc_id,UNPC_SEX,SEX_FEMALE;
	setunitdata .@npc_id,UNPC_HEADTOP,142;
	setunitdata .@npc_id,UNPC_HEADMIDDLE,92;
	setunitdata .@npc_id,UNPC_HAIRSTYLE,2;
	setunitdata .@npc_id,UNPC_HAIRCOLOR,2;

	// Restock
	npcshopupdate "market_resonance_stone",100003,2000000,99999;
	npcshopupdate "market_resonance_stone",100004,2000000,99999;
	end;
}


//============================================================
//= Illusion of Teddy Bear
//============================================================

ein_d02_i,177,158,3	script	Bear Wanting Illusion Stone#ITB	4_NASARIAN,{
	.@item_name$ = getitemname(25271); // IllusionStone
	mes "[Bear Wanting " + .@item_name$ + "]";
	mes "If you give me an " + .@item_name$ + ", I'll do something nice for you!";
	next;
	switch( select( "Reason for being here", "Good thing [Illusion equipment]", "Quit" ) ) {
	case 1:
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "I don't know why...!";
		mes "The other teddy bears were in line, so I was in line too!";
		mes "Whoaaaaaa!!!!";
		mes "I didn't know it was here!";
		next;
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "A teddy bear with an " + .@item_name$ + "!";
		mes "Strong!";
		mes "I don't have any " + .@item_name$ + "s!";
		mes "Less... stronger...";
		next;
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "What are " + .@item_name$ + "s?";
		mes "I don't know but I want some!";
		close;
	case 2:
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "Makes you good with what you have!";
		next;
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "If you want to do well, bring equipment refined to ^ff0000+9 or higher. It'll all disappear... haha?^000000";
		next;
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "^ff0000" + .@item_name$ + "^000000!!! " + .@item_name$ + " is the most important thing, but... other materials are also needed.";
		mes "For every good thing you need another good thing.";
		next;
		close2;
		callshop( "barter_ill_teddy" );
		end;
	case 3:
		mes "[Bear Wanting " + .@item_name$ + "]";
		mes "Okay...";
		close;
	}
	end;
}